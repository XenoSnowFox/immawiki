plugins {
    // Compiling
    id "java"
    id "io.miret.etienne.sass" version "1.4.2" // SASS Compiler

    // Formatting
    id 'com.diffplug.spotless' version "6.25.0"

    // Code Coverage
    id "jacoco"

    // Testing
    id "info.solidsoft.pitest" version "1.9.11" // Mutation Testing
    id "com.adarshr.test-logger" version "4.0.0" // Test Logger
}

group = "com.xenosnowfox.immawiki"
version = "0.0.0"
defaultTasks "spotlessApply", "clean", "compileSass", "copyFonts", "shadowJar"

repositories {
    mavenCentral()
    maven {
        url = "https://s3-us-west-2.amazonaws.com/dynamodb-local/release"
    }
}

allprojects {
    group = rootProject.group
    version = rootProject.version

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    repositories {
        mavenCentral()
        maven {
            url = "https://s3-us-west-2.amazonaws.com/dynamodb-local/release"
        }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "com.adarshr.test-logger"

    project.buildDir(new File(rootProject.projectDir, "build/modules/" + project.name))

    dependencies {

        // Jackson Data Modules
        implementation "com.fasterxml.jackson.core:jackson-databind:2.18.0"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.14.1"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.1"
        implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:2.14.1"
        implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.0"

        // project lombok
        compileOnly "org.projectlombok:lombok:1.18.34"
        annotationProcessor "org.projectlombok:lombok:1.18.34"
        testCompileOnly "org.projectlombok:lombok:1.18.34"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.34"

        // JUnit5 Testing Dependencies
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.11.1"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:5.11.1"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.11.1"
    }

    jacoco {
        toolVersion = "0.8.12"
//	reportsDir = file("$buildDir/jacoco")
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport // report is always generated after tests run
    }


    compileJava {
        dependsOn "spotlessApply"
    }

    spotless {
        java {
            importOrder()
            removeUnusedImports()
            googleJavaFormat().reflowLongStrings()
            formatAnnotations()
        }
        format("styling") {
            target("src/*/sass/**/*.css", "src/*/sass/**/*.scss")
        }
    }
}


sass {
//    version = '1.26.10'
    version = '1.80.4'
    directory = file ("${rootDir}/.gradle/sass")
    baseUrl = 'https://github.com/sass/dart-sass/releases/download'
}

compileSass {
    outputDir = project.file ("${projectDir}/build/modules/immawiki-static-resources/styles/")
    sourceDir = project.file ("${projectDir}/sass")
    style = compressed
    noCharset ()
    noErrorCss ()
    sourceMap = none
    sourceMapUrls = relative
}

task copyFonts(type: Copy) {
    from 'fonts'
    into 'build/modules/immawiki-static-resources/fonts'
}
compileJava.dependsOn copyFonts